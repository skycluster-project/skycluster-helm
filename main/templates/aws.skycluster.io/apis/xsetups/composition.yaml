apiVersion: apiextensions.crossplane.io/v1
kind: Composition
metadata:
  name: xsetups.aws.skycluster.io
spec:
  compositeTypeRef:
    apiVersion: aws.skycluster.io/v1alpha1
    kind: XSetup
  mode: Pipeline
  pipeline:
    - step: pull-extra-resources
      functionRef:
        name: function-extra-resources
      input:
        apiVersion: extra-resources.fn.crossplane.io/v1beta1
        kind: Input
        spec:
          extraResources:
            - kind: ProviderConfig
              into: ProviderConfig
              apiVersion: aws.upbound.io/v1beta1
              type: Selector
              selector:
                maxMatch: 1
                minMatch: 1
                matchLabels:
                  - key: skycluster.io/managed-by
                    type: Value
                    value: skycluster
            - kind: XSetup
              into: SkySetups
              apiVersion: skycluster.io/v1alpha1
              type: Selector
              selector:
                maxMatch: 1
                minMatch: 1
                matchLabels:
                  - key: skycluster.io/managed-by
                    type: Value
                    value: skycluster
    - step: resources
      functionRef:
        name: function-kcl
      input:
        apiVersion: krm.kcl.dev/v1alpha1
        kind: KCLInput
        metadata:
          name: basic
        spec:
          dependencies: |
            provider-aws = { git = "https://github.com/skycluster-project/kcl-modules",version = "0.0.1" }
            provider-kubernetes = { git = "https://github.com/skycluster-project/kcl-modules",version = "0.0.1" }
            helper = { git = "https://github.com/skycluster-project/kcl-modules",version = "0.0.1" }
          source: |
            import helper.v1alpha1.main as helper
            import provider_aws.ec2.v1beta1 as ec2v1beta1

            oxr = option("params").oxr # observed composite resource
            ocds = option("params")?.ocds # observed composed resources
            # _dxr = option("params").dxr # desired composite resource
            # dcds = option("params").dcds # desired composed resources

            # assert oxr.metadata?.labels is not Undefined, "At least one label must be specified"
            # assert "skycluster.io/managed-by" in oxr.metadata.labels, "Label 'skycluster.io/managed-by' must be specified"

            ctx = option("params")?.ctx
            assert ctx is not Undefined, "Context must be provided in the params"

            _extraRes = ctx["apiextensions.crossplane.io/extra-resources"]
            assert _extraRes is not Undefined, "Extra resources must be provided in the context"

            _provCfg = _extraRes["ProviderConfig"][0]
            _skySetup = _extraRes["SkySetups"][0]

            _ns = _skySetup.spec.namespace or "skycluster-system"

            _oxrProvRegion = oxr.spec.providerRef.region
            _oxrProvZone = oxr.spec.providerRef.zones?.primary
            _oxrProvPlatform = oxr.spec.providerRef.platform
            _oxrAppId = oxr.spec.applicationId or Undefined

            assert _oxrProvRegion and _oxrProvZone and _oxrProvPlatform, "Provider region, primary zone, platform must be specified"
            assert len(oxr.spec.subnets) > 0, "At least one subnet must be specified in the XSkyCluster spec"

            _vpcCidr = oxr.spec.vpcCidr
            assert _vpcCidr, "VPC CIDR must be specified in the XSkyCluster spec"
            _vpcCidrParts = _vpcCidr.split("/")
            _vpcCidrIp = _vpcCidrParts[0]
            _vpcCidrOctets = _vpcCidrIp.split(".")
            _vpcCidrFirst = _vpcCidrOctets[0]
            _vpcCidrSecond = _vpcCidrOctets[1]
            _vpcCidrThird = _vpcCidrOctets[2]
            _vpcCidrMask = _vpcCidrParts[1]

            _tcpPorts = oxr.spec.tcpPorts
            _udpPorts = oxr.spec.udpPorts

            _items = []
            _items += [ _helper_vpc() ]

            _subnets = []
            _subnets +=  [{
                "type" = sub.type,
                "zone" = sub.zone,
                "cidrBlock" = sub.cidr,
            } for i, sub in oxr.spec.subnets]

            _items += [_helper_subnet(i, s) for i, s in _subnets]

            _items += [_helper_network_interface()]

            _items += [_helper_internet_gw()]

            #
            # Route tables
            #
            _items += [_helper_route_table("public")]
            _items += [_helper_route_table("private")]

            _items += [_helper_route_table_association(i, s) for i, s in _subnets]

            _items += [_helper_route("public")]
            _items += [_helper_route("private")]

            # Security group configuration
            _items += [_helper_sg()]

            _items += [_helper_sg_rule_outbound()]
            _items += [_helper_sg_rule_inbound_icmp()]
            _items += [_helper_sg_rule_inbound_internal()]

            #
            # Inbound TCP security group rules
            #
            _tcpPortDefaults = [
              {"fromPort" = 30000, "toPort" = 32767},
              {"fromPort" = 32768, "toPort" = 60999},
              {"fromPort" = 15000, "toPort" = 15100} # istio envoy
            ]

            _tcpPortDefaults = (_tcpPortDefaults if _tcpPortDefaults else []) + [
              {"fromPort" = port, "toPort" = port} 
                for port in [22 80 443 6443 8080 9100 179 4789 5473 2379 10250 8000 8001 9000 9001]
            ]
            _tcpPortsToUse = _tcpPorts if _tcpPorts else _tcpPortDefaults

            _items += [_helper_sg_rule(port, "tcp") for port in _tcpPortsToUse]

            _udpPortDefaults = [
              {"fromPort" = port, "toPort" = port} 
                for port in [3478 4500 4789 4800 8472 41641 51820 51821]
            ]
            _udpPortsToUse = _udpPorts if _udpPorts else _udpPortDefaults

            #
            # Inbound UDP security group rules
            #
            _items += [_helper_sg_rule(port, "udp") for port in _udpPortsToUse]

            # KeyPair configuration
            _items += [_helper_keypair("keypair")] if oxr.spec?.publicKey or ocds?["keypair"] else []

            # Elastic IP configuration
            # _items += [_helper_eip()]

            _ami = oxr.spec.gateway?.image 
            assert _ami, "Gateway image must be specified in the XSkyCluster spec"

            _it = oxr.spec.gateway?.flavor
            assert _it, "Gateway flavor must be specified in the XSkyCluster spec"

            _keyPairId = ocds?.keypair?.Resource?.status?.atProvider?.keyPairId 
            _keypairName = ocds?.keypair?.Resource?.status?.atProvider?.id 

            _userData = oxr.spec.gateway?.userData 
            _userData = _userData.replace("__SUBNETCIDR__", _vpcCidr) if _userData else Undefined

            _networkIfId = ocds?["networkInterface"]?.Resource?.status?.atProvider?.id
            _items += [ _helper_ec2_instance(_networkIfId) ] if _networkIfId or ocds?["gwInstance"] else []

            _keyPairId = ocds?.keypair?.Resource?.status?.atProvider?.keyPairId 
            _keypairName = ocds?.keypair?.Resource?.status?.atProvider?.id 
            _vpcId = ocds?.vpc?.Resource?.status?.atProvider?.id
            _ocdsSubnets = [v.Resource for k, v in ocds if k.find("subnet") != -1 and v.Resource]
            _publicIp = ocds?["gwInstance"]?.Resource?.status?.atProvider?.publicIp
            _privateIp = ocds?["gwInstance"]?.Resource?.status?.atProvider?.privateIp

            dxr = {
              **option("params").dxr,
              status = {
                if _keyPairId and _keypairName:
                  keypair = {
                    name = _keyPairId
                    id = _keypairName
                  }
                if _vpcId:
                  vpc = {
                    id = _vpcId
                    cidrBlock = _vpcCidr 
                  }
                subnets = [
                  {
                    type = "public" if v.metadata?.labels?["skycluster.io/public-subnet"] and v.metadata?.labels?["skycluster.io/public-subnet"] == "true" else "private"
                    id = v.status?.atProvider?.id
                    cidrBlock = v.status?.atProvider?.cidrBlock
                  } for _, v in _ocdsSubnets if v
                ]
                if _publicIp or _privateIp:
                  gateway = {
                    publicIp = _publicIp
                    privateIp = _privateIp
                  } 
              }
            }

            #
            ############### Helper Mixins ################
            #

            # Collect all resources into a list for output
            items = [*_items, dxr]

            _helper_ec2_instance = lambda _netIfId {
              ec2v1beta1.Instance{
                metadata = {
                  annotations = {
                    **helper._set_resource_name("gwInstance"),
                  }
                },
                spec = {
                  forProvider = {
                    ami = _ami,
                    instanceType = _it,
                    sourceDestCheck = False,
                    region = _oxrProvRegion,
                    keyName = _keypairName,
                    rootBlockDevice = [{
                      volumeSize = oxr.spec?.gateway?.volumeSize or 20,
                      volumeType = oxr.spec?.gateway?.volumeType or "gp2",
                    }]
                    networkInterface = [
                      {
                        networkInterfaceId = \
                          ocds?["gwInstance"]?.Resource?.status?.atProvider?.networkInterface?[0]?.networkInterfaceId \
                            or _netIfId
                        deviceIndex = 0,
                        deleteOnTermination = False
                      }
                    ]
                    userData = _userData
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_eip = lambda {
              ec2v1beta1.EIP {
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                  }
                  annotations = {
                    **helper._set_resource_name("elasticIP"),
                  }
                },
                spec = {
                  forProvider = {
                    domain = "vpc"
                    networkBorderGroup = ""
                    networkInterfaceSelector = {
                      matchControllerRef = True
                      matchLabels = {
                        "skycluster.io/interface-id" = "network-interface",
                      }
                    }
                    region = _oxrProvRegion,
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_keypair = lambda s {
              ec2v1beta1.KeyPair{
                metadata = {
                  labels = { 
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId) 
                  }
                  annotations = {
                    **helper._set_resource_name(s),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    publicKey = oxr.spec?.publicKey,
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_vpc = lambda {
              ec2v1beta1.VPC{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                  }
                  annotations = {
                    **helper._set_resource_name("vpc"),
                  }
                }
                spec ={
                  forProvider ={
                    cidrBlock = "{0}.{1}.0.0/{2}".format(_vpcCidrFirst, _vpcCidrSecond, "16"),
                    region = _oxrProvRegion,
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_subnet = lambda i, s {
              ec2v1beta1.Subnet{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, "", _oxrAppId),
                    "skycluster.io/public-subnet" = "false" if s.type == "private" else "true",
                    "skycluster.io/subnet-zone" = s.zone
                    "skycluster.io/subnet-id" = "subnet-{}-{}".format("private" if s.type == "private" else "public", i)
                  }
                  annotations = {
                    **helper._set_resource_name("subnet{}{}".format("Private" if s.type == "private" else "Public", i)),
                  }
                },
                spec = {
                  forProvider = {
                    cidrBlock = s.cidrBlock,
                    region = _oxrProvRegion,
                    availabilityZone = s.zone,
                    vpcIdSelector.matchControllerRef = True
                    mapPublicIpOnLaunch = True if s.type == "public" else False,
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_network_interface = lambda {
              ec2v1beta1.NetworkInterface{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                    "skycluster.io/interface-id" = "network-interface",
                  }
                  annotations = {
                    **helper._set_resource_name("networkInterface"),
                  }
                },
                spec = {
                  forProvider = {
                    sourceDestCheck = False,
                    region = _oxrProvRegion,
                    subnetIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/public-subnet" = "true",
                      },
                    },
                    securityGroupSelector.matchControllerRef = True,
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_internet_gw = lambda {
              ec2v1beta1.InternetGateway{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId) 
                  }
                  annotations = {
                    **helper._set_resource_name("internet-gateway"),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    vpcIdSelector.matchControllerRef = True
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_route_table = lambda t {
              ec2v1beta1.RouteTable{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                    "skycluster.io/public-route-table" = "true" if t == "public" else "false",
                  }
                  annotations = {
                    **helper._set_resource_name("{}-route-table".format("public" if t == "public" else "private")),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    vpcIdSelector.matchControllerRef = True
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_route_table_association = lambda i, s {
              ec2v1beta1.RouteTableAssociation{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                    "skycluster.io/public-subnet" = "true" if s.type == "public" else "false",
                  }
                  annotations = {
                    **helper._set_resource_name("{}-route-table-assoc-{}".format("public" if s.type == "public" else "private", i)),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    routeTableIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/public-route-table" = "true" if s.type == "public" else "false",
                      },
                    },
                    subnetIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/subnat-id" = "subnet-{}-{}".format("private" if s.type == "private" else "public", i)
                      },
                    },
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_route = lambda t {
              ec2v1beta1.Route{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                    "skycluster.io/public-route-table" = "true" if t == "public" else "false",
                  }
                  annotations = {
                    **helper._set_resource_name("{}-route".format("public" if t == "public" else "private")),
                  }
                },
                spec = {
                  forProvider = {
                    destinationCidrBlock = "0.0.0.0/0",
                    region = _oxrProvRegion,
                    routeTableIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/public-route-table" = "true" if t == "public" else "false",
                      },
                    },
                    if t == "public":
                      gatewayIdSelector.matchControllerRef = True,
                    if t == "private":
                      networkInterfaceIdSelector = {
                        matchControllerRef = True,
                        matchLabels = {
                          "skycluster.io/interface-id" = "network-interface",
                        },
                      },
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_sg = lambda {
              ec2v1beta1.SecurityGroup{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                    "skycluster.io/secgroup-type" = "default",
                  }
                  annotations = {
                    **helper._set_resource_name("security-group"),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    vpcIdSelector.matchControllerRef = True
                    tags = {
                      **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                      "skycluster.io/secgroup-type" = "default",
                    }
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_sg_rule_outbound = lambda {
              ec2v1beta1.SecurityGroupRule{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                  }
                  annotations = {
                    **helper._set_resource_name("security-group-rule-outbound"),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    securityGroupIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/secgroup-type" = "default",
                      },
                    },
                    description = "Allow all outbound traffic",
                    protocol = "-1",
                    fromPort = 0,
                    toPort = 0,
                    type = "egress",
                    cidrBlocks = ["0.0.0.0/0"],
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_sg_rule_inbound_icmp = lambda {
              ec2v1beta1.SecurityGroupRule{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                  }
                  annotations = {
                    **helper._set_resource_name("security-group-rule-inbound-icmp"),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    securityGroupIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/secgroup-type" = "default",
                      },
                    },
                    description = "Allow all ICMP traffic",
                    protocol = "icmp",
                    fromPort = -1,
                    toPort = -1,
                    type = "ingress",
                    cidrBlocks = ["0.0.0.0/0"],
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_sg_rule = lambda port, proto {
              ec2v1beta1.SecurityGroupRule{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                  }
                  annotations = {
                    **helper._set_resource_name("security-group-rule-inbound-{}-{}-{}".format(proto, port.fromPort, port.toPort)),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    securityGroupIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/secgroup-type" = "default",
                      },
                    },
                    description = "Inbound {}: {}-{}".format(proto, port.fromPort, port.toPort),
                    protocol = proto,
                    fromPort = port.fromPort,
                    toPort = port.toPort,
                    type = "ingress",
                    cidrBlocks = [ "0.0.0.0/0" ],
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

            _helper_sg_rule_inbound_internal = lambda {
              ec2v1beta1.SecurityGroupRule{
                metadata = {
                  labels = {
                    **helper._set_default_labels(_oxrProvPlatform, _oxrProvRegion, _oxrProvZone, _oxrAppId)
                  }
                  annotations = {
                    **helper._set_resource_name("security-group-rule-inbound-self"),
                  }
                },
                spec = {
                  forProvider = {
                    region = _oxrProvRegion,
                    securityGroupIdSelector = {
                      matchControllerRef = True,
                      matchLabels = {
                        "skycluster.io/secgroup-type" = "default",
                      },
                    },
                    description = "Allow all internal traffic within the security group",
                    self = True
                    protocol = "-1",
                    fromPort = 0,
                    toPort = 0,
                    type = "ingress",
                  },
                  providerConfigRef = {
                    name = _provCfg.metadata.name,
                  },
                }
              }
            }

    - step: crossplane-contrib-function-auto-ready
      functionRef:
        name: function-auto-ready
